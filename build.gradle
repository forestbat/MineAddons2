buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "v${mod_version}-mc${minecraft_version}"
group = "com.iamshift.mineaddons" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "MineAddons"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

repositories {
	maven {
		url 'http://dvs1.progwml6.com/files/maven'
	}
	maven {
		url = "http://maven.covers1624.net"
	}
	maven {
		url = "http://tehnut.info/maven"
	}
	maven {
        url 'http://tehnut.info/maven/'
    }
}

minecraft {
    version = "1.12.2-14.23.1.2604"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
	
	useDepAts = true
}

dependencies {
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.1.21"
	deobfCompile "slimeknights:TConstruct:1.12-2.7.4.34"
	deobfCompile "mcp.mobius.waila:Hwyla:1.8.23-B38_1.12"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	exclude "**/Thumbs.db"
	exclude "**/*.psd"
	
	manifest {
		attributes 'FMLAT': 'mineaddons_at.cfg'
		attributes ("FMLCorePlugin": "com.iamshift.mineaddons.init.ModInject", "FMLCorePluginContainsFMLMod": "true")
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty('keyStore')
	}
	
	keyStore = project.findProperty('keyStore')
    alias = project.findProperty('alias')
    storePass = project.findProperty('storePass')
    keyPass = project.findProperty('keyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar